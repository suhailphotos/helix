# --- Optional: p10k instant prompt if Starship is missing (must be at top)
if ! command -v starship >/dev/null 2>&1; then
  if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
  fi
fi

# Bring in zprofile for non-login interactive shells
if [[ -o interactive && ! -o login && -r "$HOME/.zprofile" ]]; then
  source "$HOME/.zprofile"
fi

# --- Orbit bootstrap (branch-pinned, repo-local)
export ORBIT_HOME="${ORBIT_HOME:-$HOME/.orbit}"
export ORBIT_REMOTE="${ORBIT_REMOTE:-https://github.com/suhailphotos/orbit.git}"
export ORBIT_BRANCH="${ORBIT_BRANCH:-main}"     # <-- pick per-machine
export ORBIT_UPDATE_STRATEGY="${ORBIT_UPDATE_STRATEGY:-hard-reset}"  # hard-reset | ff-only

# First-time install
if [[ ! -d "$ORBIT_HOME/.git" ]]; then
  git clone --quiet --depth 1 --branch "$ORBIT_BRANCH" "$ORBIT_REMOTE" "$ORBIT_HOME"
else
  # Ensure local checkout tracks the branch named above and update it
  (
    cd "$ORBIT_HOME" || exit 0
    git fetch --quiet origin

    # Switch/create local branch that tracks origin/$ORBIT_BRANCH
    if git show-ref --verify --quiet "refs/heads/$ORBIT_BRANCH"; then
      git switch --quiet "$ORBIT_BRANCH" 2>/dev/null || git checkout -q "$ORBIT_BRANCH"
    else
      git switch --quiet -c "$ORBIT_BRANCH" --track "origin/$ORBIT_BRANCH" \
        2>/dev/null || git checkout -q -B "$ORBIT_BRANCH" "origin/$ORBIT_BRANCH"
    fi

    # Update to remote
    case "$ORBIT_UPDATE_STRATEGY" in
      hard-reset) git reset --hard --quiet "origin/$ORBIT_BRANCH" ;;
      ff-only)    git merge --ff-only --quiet "origin/$ORBIT_BRANCH" || true ;;
    esac
  ) >/dev/null 2>&1 &!
fi

# Source Orbit
source "$ORBIT_HOME/core/bootstrap.zsh"

